(ns tests.basilisp.core.test-namespace-fns
  (:require
   [basilisp.set :as set]
   [basilisp.test :refer [deftest is testing]]))

(def ^:redef redeffable :a)
(def not-redeffable :b)

(deftest with-redefs-test
  (testing "with-redefs cannot redef Vars without ^:redef metadata"
    (is (thrown? basilisp.lang.exception/ExceptionInfo
                 (with-redefs [not-redeffable :c]
                   not-redeffable)))
    (is (thrown? basilisp.lang.exception/ExceptionInfo
                 (with-redefs [redeffable     :d
                               not-redeffable :c]
                   not-redeffable))))

  (testing "with-redefs resets to the original root"
    (is (= :c (with-redefs [redeffable :c] redeffable)))
    (is (= :a redeffable)))

  (testing "with-redefs resets to the original root even after an exception"
    (is (thrown? basilisp.lang.exception/ExceptionInfo
                 (with-redefs [redeffable :c]
                   (throw
                    (ex-info "This is an exception" {})))))
    (is (= :a redeffable))))

(deftest require-test
  (is (thrown? basilisp.lang.exception/ExceptionInfo (require '[basilisp.set :refer [non-existent-var]]))))

(deftest requiring-resolve-test
  (is (thrown? basilisp.lang.exception/ExceptionInfo (requiring-resolve 'a-symbol)))
  (is (thrown? basilisp.lang.exception/ExceptionInfo (requiring-resolve :a-keyword)))
  (is (thrown? basilisp.lang.exception/ExceptionInfo (requiring-resolve :ns.qualified/kw)))
  (is (thrown? python/ImportError (requiring-resolve 'not.a.real.ns/sym)))
  (is (nil? (requiring-resolve 'basilisp.shell/sholl)))
  ;; this check should succeed (in spite of the missing top-level require) because
  ;; the previous assertion will import 'basilisp.shell even though 'sholl is not
  ;; a valid symbol in that namespace
  (is (= (resolve 'basilisp.shell/sh) (requiring-resolve 'basilisp.shell/sh))))

(deftest importing-resolve-test
  (is (thrown? basilisp.lang.exception/ExceptionInfo (importing-resolve 'a-symbol)))
  (is (thrown? basilisp.lang.exception/ExceptionInfo (importing-resolve :a-keyword)))
  (is (thrown? basilisp.lang.exception/ExceptionInfo (importing-resolve :ns.qualified/kw)))
  (is (thrown? python/ImportError (importing-resolve 'not.a.real.ns/sym)))
  (is (nil? (importing-resolve 'logging/fakeGetLogger)))
  ;; this check should succeed (in spite of the missing top-level require) because
  ;; the previous assertion will import 'basilisp.shell even though 'sholl is not
  ;; a valid symbol in that namespace
  (is (= (python/getattr (get sys/modules "logging") "getLogger")
         (importing-resolve 'logging/getLogger))))

(deftest ns-resolve-test
  (is (= #'basilisp.core/apply (ns-resolve *ns* 'apply)))
  (is (nil? (ns-resolve *ns* 'xyz)))

  (is (= #'basilisp.set/union (ns-resolve *ns* 'basilisp.set/union)))
  (is (= #'basilisp.set/union (ns-resolve *ns* 'set/union)))
  (is (nil? (ns-resolve *ns* 'basilisp.set/xyz)))
  (is (nil? (ns-resolve *ns* 'set/xyz)))

  (is (= #'basilisp.test/is (ns-resolve *ns* 'is)))
  (is (nil? (ns-resolve *ns* '*test-section*)))
  (is (= #'basilisp.test/*test-section* (ns-resolve (the-ns 'basilisp.test) '*test-section*)))

  (is (nil? (ns-resolve *ns* 'if))))

(deftest intern-test
  (let [ns-sym (gensym "intern-test-ns")
        ns0 (create-ns ns-sym)]
    (testing "unbound"
      (let [sym (gensym "intern-test")
            v (intern ns0 sym)
            {:keys [name ns] :as m} (meta v)]
        (is (= ns0 ns))
        (is (= sym name))
        (is (= false (.-is-bound v)))))

    (testing "unbound ns-sym"
      (let [sym (gensym "intern-test")
            v (intern ns-sym sym)
            {:keys [name ns] :as m} (meta v)]
        (is (= ns0 ns))
        (is (= sym name))
        (is (= false (.-is-bound v)))))

    (testing "bound"
      (let [sym (gensym "intern-test")
            v (intern ns0 sym 5)
            {:keys [name ns] :as m} (meta v)]
        (is (= ns0 ns))
        (is (= sym name))
        (is (= true (.-is-bound v)))
        (is (= 5 @v))
        (is (= v (find-var (symbol (str ns0) (str sym))))))))

  (testing "ns-not-exists"
    (is (thrown? python/AttributeError (intern 'ns-non-existant 'xyz)))))
